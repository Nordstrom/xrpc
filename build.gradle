allprojects {
  repositories {
    mavenCentral()
    jcenter()
  }

  apply plugin: 'java'
  apply plugin: 'checkstyle'

  tasks.withType(Javadoc) {
    // Don't print missing javadoc warnings; checkstyle covers these.
    options.addBooleanOption('Xdoclint:-missing', true)
  }

  task resolveDependencies {
    setDescription "Resolves all projects dependencies from the repository."
    setGroup "Build Server"

    doLast {
      rootProject.allprojects { project ->
        project.buildscript.configurations.forEach { configuration ->
          if (configuration.canBeResolved) {
            configuration.resolve()
          }
        }

        project.configurations.forEach { configuration ->
          if (configuration.canBeResolved) {
            configuration.resolve()
          }
        }
      }
    }
  }

  checkstyle {
    toolVersion '8.3'
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
  }
}
