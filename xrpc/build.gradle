buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
  }
}

plugins {
  id 'com.github.sherter.google-java-format' version '0.6'
  id 'net.researchgate.release' version '2.4.0'
  id 'com.github.spotbugs' version '1.6.1'
  id 'me.champeau.gradle.jmh' version '0.4.4'
}

release {
  preTagCommitMessage = 'Updating to release version '
  tagCommitMessage = 'New release: '
  newVersionCommitMessage = 'Creating new SNAPSHOT version '
  git {
    pushToRemote = 'upstream'
  }
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'

// Junit 5 gradle runner.
apply plugin: 'org.junit.platform.gradle.plugin'

group = 'com.nordstrom.xrpc'
archivesBaseName = 'xrpc'

description = """Simple, production ready Java API server built on top of functional composition."""

sourceCompatibility = 1.10
targetCompatibility = 1.10
tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs += [
    '-XDignore.symbol.file',
    '-Xlint:all',
    '-Xlint:-deprecation',
    '-Xlint:-unchecked',
    '-Xlint:-rawtypes',
    '-Xlint:-serial',
    '-Xlint:-processing',
    '--add-exports',
    'java.base/sun.security.x509=ALL-UNNAMED',
    '--add-exports',
    'java.base/java.nio=ALL-UNNAMED',
    '--add-exports',
    'java.base/sun.security.util=ALL-UNNAMED',
    '--add-exports',
    'java.base/com.sun.tools.javac.code=ALL-UNNAMED',
    '--add-exports',
    'java.base/sun.security.validator=ALL-UNNAMED',
    '--add-exports',
    'java.base/sun.security.provider.certpath=ALL-UNNAMED',
    '-Werror']
  options.fork = true
  options.forkOptions.executable = 'javac'
}

repositories {
  mavenCentral()
  jcenter()
}

configurations {
  // This is added to run the alpn agent
  agent
  runtime.extendsFrom(agent)
}

dependencies {
  compile files('../lib/lombok-edge.jar')

  compile 'com.google.auto.value:auto-value:1.5.3'
  compile 'com.google.guava:guava:21.0'
  compile 'com.squareup.okhttp3:okhttp:3.10.0'
  compile 'com.typesafe:config:1.3.1'
  compile 'com.xjeffrose:xio:0.13.0'
  compile 'com.google.protobuf:protobuf-java:3.0.0'
  compile 'com.google.protobuf:protobuf-java-util:3.0.0'
  compile 'io.dropwizard.metrics:metrics-core:3.2.5'
  compile 'io.dropwizard.metrics:metrics-healthchecks:3.2.5'
  compile 'io.dropwizard.metrics:metrics-json:3.2.5'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.9.4'
  compile 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.9.4'
  compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.4'
  compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.4'
  compile 'io.netty:netty-all:4.1.19.Final'
  compile 'io.netty:netty-tcnative-boringssl-static:2.0.7.Final'
  compile 'io.netty:netty-transport-native-epoll:4.1.19.Final'
  compile 'io.netty:netty-transport-native-kqueue:4.1.19.Final'
//  compile 'org.projectlombok:lombok:1.16.20'
  compile 'org.slf4j:slf4j-api:1.7.25'
  testCompile 'com.xjeffrose:xio-test:0.13.5'
  testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.1'
  testCompile 'org.apiguardian:apiguardian-api:1.0.0'
  testCompile 'org.mockito:mockito-core:2.13.0'
  testCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'
  testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
  testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
  testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.1'
  runtimeOnly 'ch.qos.logback:logback-classic:1.1.7'
  runtimeOnly 'ch.qos.logback:logback-core:1.1.7'
  runtimeOnly 'org.codehaus.groovy:groovy-all:2.4.1'

  // alpn agent necessary for http2 testing with okhttp
  agent 'org.mortbay.jetty.alpn:jetty-alpn-agent:2.0.7'

  jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.15'


}

junitPlatformTest {
  doFirst {
    // configure alpn agent
    project.configurations.agent.each { agent ->
      jvmArgs += ["-javaagent:${agent.path}"]
    }
  }
}

sourceSets {
  main {
    java {
      srcDirs 'build/generated/source/proto/main/java'
    }
  }
  test {
    java {
      srcDirs 'build/generated/source/proto/test/java'
    }
  }
}

// Run with the latest checkstyle version. Required for our checkstyle.xml.
checkstyle {
  toolVersion '8.3'
}

spotbugs {
  toolVersion = '3.1.2'
  ignoreFailures = true
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  extraArgs = ['-html:fancy-hist.xsl']
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

// Configure protobuf build.
protobuf {
  // Configure the protoc executable to use the official distribution.
  protoc {
    artifact = 'com.google.protobuf:protoc:3.0.0'
  }
}

// Add Javadoc + sources jars to artifacts list.
task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}
task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

// Run javadoc as part of the `check` task.
check.dependsOn(javadoc)

// include: default strategy where the last duplicate file 'wins'.
jmh {
  duplicateClassesStrategy = 'include'
}

def pomExtra = {
  scm {
    connection 'scm:git:git@github.com:Nordstrom/xrpc.git'
    developerConnection 'scm:git:git@github.com:Nordstrom/xrpc.git'
    url 'https://github.com/Nordstrom/xrpc'
  }

  licenses {
    license {
      name 'The Apache License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    }
  }

  developers {
    developer {
      id 'jkinkead'
      name 'Jesse Kinkead'
      email 'jesse.kinkead@nordstrom.com'
    }
    developer {
      id 'xjdr'
      name 'Jeff Rose'
      email 'jeff.rose@nordstrom.com'
    }
    developer {
      id 'andyday'
      name 'Andy Day'
      email 'andy.day@nordstrom.com'
    }
  }
}

publishing {
  publications {
    xrpcMaven(MavenPublication) {
      from components.java
      groupId group
      artifactId archivesBaseName
      version version
      artifact sourcesJar
      artifact javadocJar

      pom.withXml {
        asNode().appendNode('description', description)
        asNode().appendNode('name', 'xrpc library')
        asNode().appendNode('url', 'https://github.com/Nordstrom/xrpc')
        asNode().children().last() + pomExtra
      }
    }
  }
}
